plugins {
    id 'com.android.library'
}

version wireguardVersionName
group groupName

android {
    compileSdk 33
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.wireguard.android.tunnel'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode wireguardVersionCode
        versionName wireguardVersionName
    }
    externalNativeBuild {
        cmake {
            path 'tools/CMakeLists.txt'
        }
    }
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    buildTypes {
        all {
            externalNativeBuild {
                cmake {
                    targets 'libwg-go.so', 'libwg.so', 'libwg-quick.so'
                    arguments "-DGRADLE_USER_HOME=${project.gradle.gradleUserHomeDir}"
                }
            }
        }
        release {
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${groupName}"
                }
            }
        }
        debug {
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${groupName}.debug"
                }
            }
        }
    }
    lint {
        disable 'LongLogTag', 'NewApi'
    }
    publishing {
        multipleVariants("release") {
            allVariants()
        }
    }
}

dependencies {
    implementation "androidx.annotation:annotation:$annotationsVersion"
    implementation "androidx.collection:collection:$collectionVersion"
    compileOnly "com.google.code.findbugs:jsr305:$jsr305Version"
    testImplementation "junit:junit:$junitVersion"
}

apply from: "publish.gradle"
