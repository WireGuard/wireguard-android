buildscript {
    ext {
        activityVersion = '1.2.2'
        agpVersion = '4.1.3'
        annotationsVersion = '1.2.0'
        appcompatVersion = '1.2.0'
        biometricVersion = '1.1.0'
        collectionVersion = '1.1.0'
        constraintLayoutVersion = '2.0.4'
        coordinatorLayoutVersion = '1.1.0'
        coreKtxVersion = '1.3.2'
        coroutinesVersion = '1.4.3'
        datastoreVersion = '1.0.0-alpha02'
        desugarVersion = '1.0.10'
        fragmentVersion = '1.3.3'
        jsr305Version = '3.0.2'
        junitVersion = '4.13.2'
        kotlinVersion = '1.4.32'
        lifecycleRuntimeKtxVersion = '2.3.0'
        materialComponentsVersion = '1.3.0'
        preferenceVersion = '1.1.1'
        zxingEmbeddedVersion = '4.2.0'

        groupName = 'com.wireguard.android'
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$agpVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
    repositories {
        google()
        mavenCentral()
    }
}

plugins {
    id "de.undercouch.download" version "4.1.1"
}

task downloadCrowdin(type: Download) {
	src 'https://crowdin.com/backend/download/project/wireguard.zip'
	dest file('build/translations.zip')
	overwrite true
}

task cleanCrowdin(type: Delete) {
	delete 'ui/src/main/res/values-*/strings.xml'
}

task extractCrowdin(type: Copy, dependsOn: ['downloadCrowdin', 'cleanCrowdin']) {
	mustRunAfter 'downloadCrowdin'
	from zipTree(file('build/translations.zip'))
	into file('build/translations')
	doFirst {
		delete 'build/translations'
	}
}

task crowdin(type: Copy, dependsOn: ['extractCrowdin']) {
    mustRunAfter 'extractCrowdin'
    from 'build/translations/wireguard-android/ui/src/main/res'
    into 'ui/src/main/res/'
    doLast {
        delete 'build/translations'
        delete 'build/translations.zip'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    configurations.all {
        // New versions use different maven coordinates and are available on Maven Central
        resolutionStrategy.dependencySubstitution {
            substitute(module("org.jetbrains.trove4j:trove4j:20160824"))
                .using(module("org.jetbrains.intellij.deps:trove4j:1.0.20181211"))
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks {
    wrapper {
        gradleVersion = "7.0"
        distributionType = Wrapper.DistributionType.ALL
        distributionSha256Sum = "81003f83b0056d20eedf48cddd4f52a9813163d4ba185bcf8abd34b8eeea4cbd"
    }
}

apply from: "version.gradle"
