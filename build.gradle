buildscript {
    ext {
        activityVersion = '1.2.3'
        agpVersion = '4.2.1'
        annotationsVersion = '1.2.0'
        appcompatVersion = '1.3.0'
        biometricVersion = '1.1.0'
        collectionVersion = '1.1.0'
        constraintLayoutVersion = '2.0.4'
        coordinatorLayoutVersion = '1.1.0'
        coreKtxVersion = '1.5.0'
        coroutinesVersion = '1.5.0'
        datastoreVersion = '1.0.0-beta01'
        desugarVersion = '1.1.5'
        fragmentVersion = '1.3.4'
        jsr305Version = '3.0.2'
        junitVersion = '4.13.2'
        kotlinVersion = '1.5.10'
        lifecycleRuntimeKtxVersion = '2.3.1'
        materialComponentsVersion = '1.3.0'
        preferenceVersion = '1.1.1'
        zxingEmbeddedVersion = '4.2.0'

        groupName = 'com.wireguard.android'
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$agpVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
    repositories {
        google()
        mavenCentral()
    }
}

plugins {
    id "de.undercouch.download" version "4.1.1"
}

task downloadCrowdin(type: Download) {
	src 'https://crowdin.com/backend/download/project/wireguard.zip'
	dest file('build/translations.zip')
	overwrite true
}

task cleanCrowdin(type: Delete) {
	delete 'ui/src/main/res/values-*/strings.xml'
}

task extractCrowdin(type: Copy, dependsOn: ['downloadCrowdin', 'cleanCrowdin']) {
	mustRunAfter 'downloadCrowdin'
	from zipTree(file('build/translations.zip'))
	into file('build/translations')
	doFirst {
		delete 'build/translations'
	}
}

task crowdin(type: Copy, dependsOn: ['extractCrowdin']) {
    mustRunAfter 'extractCrowdin'
    from 'build/translations/wireguard-android/ui/src/main/res'
    into 'ui/src/main/res/'
    doLast {
        delete 'build/translations'
        delete 'build/translations.zip'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks {
    wrapper {
        gradleVersion = "7.1"
        distributionType = Wrapper.DistributionType.ALL
        distributionSha256Sum = "a9e356a21595348b6f04b024ed0b08ac8aea6b2ac37e6c0ef58e51549cd7b9cb"
    }
}

apply from: "version.gradle"
